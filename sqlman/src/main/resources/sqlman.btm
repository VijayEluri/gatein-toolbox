HELPER org.sqlman.SQLHelper

##########################################################################
# JDBC monitoring

RULE SQLMan enter JDBC PreparedStatement executeQuery
INTERFACE ^java.sql.PreparedStatement
METHOD executeQuery()
AT ENTRY
IF TRUE
DO enter("jdbcquery")
ENDRULE

RULE SQLMan leave JDBC PreparedStatement executeQuery
INTERFACE ^java.sql.PreparedStatement
METHOD executeQuery()
AT EXIT
IF TRUE
DO leave("jdbcquery")
ENDRULE

RULE SQLMan enter JDBC PreparedStatement executeUpdate
INTERFACE ^java.sql.PreparedStatement
METHOD executeUpdate()
AT ENTRY
IF TRUE
DO enter("jdbcupdate")
ENDRULE

RULE SQLMan leave JDBC PreparedStatement executeUpdate
INTERFACE ^java.sql.PreparedStatement
METHOD executeUpdate()
AT EXIT
IF TRUE
DO leave("jdbcupdate")
ENDRULE

RULE SQLMan enter JDBC PreparedStatement execute
INTERFACE ^java.sql.PreparedStatement
METHOD execute()
AT ENTRY
IF TRUE
DO enter("jdbc")
ENDRULE

RULE SQLMan leave JDBC PreparedStatement execute
INTERFACE ^java.sql.PreparedStatement
METHOD execute()
AT EXIT
IF TRUE
DO leave("jdbc")
ENDRULE

##########################################################################
# JCR monitoring

RULE SQLMan enter JCR Query execute
INTERFACE ^javax.jcr.query.Query
METHOD execute()
AT ENTRY
IF TRUE
DO enter("jcrquery")
ENDRULE

RULE SQLMan leave JCR Query execute
INTERFACE ^javax.jcr.query.Query
METHOD execute()
AT EXIT
IF TRUE
DO leave("jcrquery")
ENDRULE

##########################################################################
# Resource bundle monitoring

RULE SQLMan enter bundle loading
CLASS java.util.ResourceBundle
METHOD loadBundle
AT ENTRY
IF TRUE
DO enter("loadbundle")
ENDRULE

RULE SQLMan leave bundle loading
CLASS java.util.ResourceBundle
METHOD loadBundle
AT EXIT
IF TRUE
DO leave("loadbundle")
ENDRULE

##########################################################################
# Servlet request integration

RULE SQLMan begin HTTP Servlet Request
CLASS ^javax.servlet.http.HttpServlet
METHOD service(javax.servlet.http.HttpServletRequest,javax.servlet.http.HttpServletResponse)
AT ENTRY
IF TRUE
DO begin($1.getRequestURL())
ENDRULE

RULE SQLMan end HTTP Servlet Request
CLASS ^javax.servlet.http.HttpServlet
METHOD service(javax.servlet.http.HttpServletRequest,javax.servlet.http.HttpServletResponse)
AT EXIT
IF TRUE
DO end()
ENDRULE
